map "http://research.philips.com/fhir/dstu3/mapping/AdToSupplyRequest" =
    "R3 Parameters of Activitydefinition.$apply to SupplyRequest"

uses "http://hl7.org/fhir/StructureDefinition/Parameters"       as source
uses "http://hl7.org/fhir/StructureDefinition/SupplyRequest" as target

group main
    input src: Parameters as source
    input tgt: SupplyRequest as target

//    "subject" : for src.parameter as p where name='subject' then {
//        do: for p.value as sbj make tgt.subject = sbj
//    }

    "practitioner" : for src.parameter as p where name='practitioner' then{
            do: for p.value as prac make tgt.requester as requester then {
                doAlso: for prac make requester.agent=prac
            }
        }

    "organization" : for src.parameter as p where name='organization' then{
            do: for p.value as org make tgt.requester as requester then {
                doAlso: for org make requester.onBehalfOf=org
            }
        }

    "encounter" : for src.parameter as p where name='encounter' then {
        do: for p.value as enc make tgt.context = enc
    }

    actded: for src.parameter as p where name='source' then {
        do: for p.resource as ad then createActDef( ad, tgt )
    }
    
endgroup

group createActDef
    input src: ActivityDefinition as source
    input tgt: SupplyRequest as target

    "status"       : for src                 make tgt.status = "draft"
    "ad.code"      : for src          as vvv check code.empty()
    "ad.timing"    : for src.timing   as vvv make tgt.occurrence = vvv
    "ad.location"  : for src.location as vvv make tgt.deliverTo = vvv
    "ad.product"   : for src.product  as vvv make tgt.orderedItem as orderedItem then {
        do: for vvv make orderedItem.item = vvv
    }
    "ad.quantity"  : for src.quantity as vvv make tgt.orderedItem as orderedItem then {
        do: for vvv make orderedItem.quantity = vvv
    }
    "ad.bodysite"  : for src          as vvv check bodysite.empty()
    "ad.dosage"    : for src          as vvv check dosage.empty()
endgroup


